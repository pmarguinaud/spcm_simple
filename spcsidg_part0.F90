SUBROUTINE SPCSIDG_PART0 (YDGEOMETRY, YDDYN, YDRIP, KSPEC2V, KMLOC, PSDIV, PSPDIVG)

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMDYN       , ONLY : TDYN
USE YOMRIP       , ONLY : TRIP
USE YOMMP0       , ONLY : MYSETW, MYSETV, MYSETN

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(IN)    :: YDDYN
TYPE(TRIP)        ,INTENT(IN)    :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KSPEC2V
INTEGER(KIND=JPIM),INTENT(IN)    :: KMLOC
REAL(KIND=JPRB),   INTENT(INOUT) :: PSDIV  (YDGEOMETRY%YRDIMV%NFLEVG,KSPEC2V)
REAL(KIND=JPRB),   INTENT(IN)    :: PSPDIVG(YDGEOMETRY%YRDIMV%NFLEVG,KSPEC2V)


INTEGER(KIND=JPIM) :: JSP, JLEV, IOFF
INTEGER(KIND=JPIM) :: IM, IN, ISTA, IEND
REAL (KIND=JPRB) :: ZBDT

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART0',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDLAP=>YDGEOMETRY%YRLAP,YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NSMAX=>YDDIM%NSMAX,NFLEVG=>YDDIMV%NFLEVG,SIHEG=>YDDYN%SIHEG,SIHEG2=>YDDYN%SIHEG2,NSPSTAF=>YDMP%NSPSTAF,&
          NPTRSVF=>YDMP%NPTRSVF, RBTS2=>YDDYN%RBTS2, TDT=>YDRIP%TDT)

!             Inversion of two tridiagonal systems (Helmholtz equation)
!                --> (SIMI*DIVprim(t+dt)).

!             Reorganisation of divergence

IM=YDLAP%MYMS(KMLOC)
ISTA=NSPSTAF(IM)
IEND=ISTA+2*(NSMAX+1-IM)-1

IOFF=NPTRSVF(MYSETV)-1
ZBDT=RBTS2*TDT

IF (IM > 0) THEN
  DO JSP=ISTA,IEND
    DO JLEV=1,NFLEVG
      IN=YDLAP%NVALUE(JSP+IOFF)
      PSDIV(JLEV,JSP)=YDLAP%RLAPIN(IN)*PSPDIVG(JLEV,JSP)-ZBDT*PSDIV(JLEV,JSP)      
    ENDDO
  ENDDO
ELSE
  DO JSP=ISTA,IEND
    DO JLEV=1,NFLEVG
      IN=YDLAP%NVALUE(JSP+IOFF)
      PSDIV(JLEV,JSP)=PSPDIVG(JLEV,JSP)-ZBDT*YDLAP%RLAPDI(IN)*PSDIV(JLEV,JSP)  
    ENDDO
  ENDDO
ENDIF

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART0',1,ZHOOK_HANDLE)
END SUBROUTINE SPCSIDG_PART0

